* Emacs Lisp Concepts


** Symbols

A *symbol* is an object with a unique name. Each symbol has four components or
"cells", which reference another object:

- print name :: the symbol's name
- value :: the symbol's current value as a variable
- function :: the symbol's function definition. Can also be a symbol, keymap,
  or keyboard macro.
- property list :: the symbol's property list

The print name cell always holds a string, and is immutable. All other cells may
be changed.


*** Defining Symbols

There are several constructs which allow defining symbols and announcing the
intent to use them in a specific way.

- defvar :: defines a symbol as a global variable. Sets the value cell
- defconst :: defines a symbol as a global constant. Sets the value cell
- defcustom :: defines a symbol as a customizable variable. Sets the value cell.
- defun :: defines a symbol as a function. Sets the function cell.
- defalias :: defines a symbol as a function alias. Sets the function cell.
- defsubst :: defines a symbol as an inline function. Sets the function cell.
- defmacro :: defines a symbol as a macro. Sets the function cell.

The above forms also provide information to Emacs on how interactive help (using
=C-h f= and =C-h v= should display information on the symbol.


*** Symbol Creation and Interning

Emacs symbols are interned in a global hash table called the *obarray*. When the
reader encounters a symbol, it hashes the name, and then searches the bucket for
that hash code in the obarray. If a match is found, that symbol object is
returned, otherwise a new entry is created, added to the obarray, and returned.

Not all symbols are interned. It is sometimes useful to have an uninterned
symbol, since it is guaranteed to not conflict with any other symbol.


*** Symbol Property List

Symbols may have an arbitrary *property list* stored in the symbol's property
list cell. The property list stores miscellaneous information about the symbol.
There is a set of standard properties which may be defined for certain symbol
types, however, arbitrary properties are allowed as well.


** Variables

A *variable* is a name used in a program to stand for a value. In Lisp, each
variable is represented by a *symbol*. The variable name is the symbol's name,
and the variable's value is the stored in the symbol's value cell.


*** Global Variables

A *global variable* has a single value throughout the entire Lisp system,
regardless of who is accessing it. The variable has only one value at a time,
and that value remains in effect until it is replaced with a new value. When the
value is replaced, no trace remains of the old value.

The value of a symbol can be specified with =set= or =setq=. =setq= is like
=set=, except that it implicitly quotes symbol names, and can be used to set
more than one variable at a time.

#+BEGIN_SRC emacs-lisp
  (set 'myvar 5)                  ; myvar = 5
  (setq myvar 6)                  ; myvar = 6
  (setq myvar 7 myothervar 8)     ; myvar = 7, myothervar = 8
#+END_SRC


*** Defining Global Variables

The =defvar= and =defconst= constructs are used to define a global
variable. These are not mandatory, however, they serve several purposes:

- Informs readers of the code that the symbol is intended to be used a certain
  way
- Informs the Lisp system how the symbol should be used (for live documentation
  and tagging purposes)

=defconst= does /not/ actually define hard constants - the Lisp system does not
prevent modifying the value of a =defconst=. The difference is primarily for
documentation purposes. Also, a =defconst= always unconditionally sets the value
of its symbol, while =defvar= only does so if the value is initially void.


*** Local Variables

Local variables exist only in some specific scope, normally a function or
special form in a program. There are several ways to define a local variable:

- As an argument to a function
- Using the =let= or =let*= special forms

Local variables *shadow* the binding of a variable at higher scopes. The
"higher-level" binding is not destroyed, but is overwritten in a specific scope
by the local variable. For example, in the example below, the variable
=the-text= is restored to its original binding when execution leaves the inner scope.

#+BEGIN_SRC emacs-lisp
  (setq the-text "global scope")
  (message the-text)                      ; prints "global scope"

  (let ((the-text "let scope"))
    (message the-text)                    ; prints "let scope"
    (let ((the-text "inner let scope"))
      (message the-text))                 ; prints "inner let scope"
    (message the-text))                   ; prints "let scope"

  (message the-text)                      ; prints "global scope"
#+END_SRC

So in general, the value of a symbol is whatever the current inner-most binding
of that symbol is. In addition, note that =setq= changes the value of the
current binding of the symbol. For example, in the sample below, the =setq= in
the =let= form changes the "let" binding, not the global binding.

#+BEGIN_SRC emacs-lisp
(setq the-text "hello")
(message the-text)		; prints "hello"

(let ((the-text "argh"))
  (message the-text)		; prints "argh"
  (setq the-text "bongo")
  (message the-text))		; prints "bongo"

(message the-text)		; prints "hello"
#+END_SRC

The =let*= special form is like =let=, except it allows the value of each
variable to refer to the values of previously let variables.

#+BEGIN_SRC emacs-lisp
  (let ((x 5)
        (y (+ x 1)))      ; error! x not defined here
    (+ x y))

  (let* ((x 5)
         (y (+ x 1)))     ; this is ok
    (+ x y))
#+END_SRC


*** Constant Variables

Some variables, including =nil=, =t=, and keyword symbols (symbols beginning
with a colon) are read-only. Any attempt to change their value results in a
run-time error.


*** Void Variables

A symbol with an unassigned value cell is a *void variable*. Attempting to use a
void variable results in a runtime error.

The =boundp= function returns =t= if a symbol has a value in its value cell. The
=makunbound= function removes the value from a symbol's value cell.


*** Buffer-Local Variables

In addition to global and local variables, Emacs Lisp also supports
*buffer-local variables*, which are variables that have a specific binding only
within a certain buffer. For example, if there is a global variable named =foo=,
you can use the =make-local-variable= function to create a local binding for
=foo= which is available only in the current buffer which "shadows" the global
binding, just like a =let= statement would.

You can also create *automatically buffer-local variables* using
=make-variable-buffer-local=. These variables are automatically buffer-local in
all buffers (even those yet to be created). They will use the default global
binding /until/ they are set with =setq=, and then create a local binding which
shadows the global binding. The default value cannot be set using =setq= in any
buffer - you must use =setq-default= to change the default value.

=fill-column= is an example of automatically buffer-local variable.

There are also file-local and directory-local variables. See the documentation
for more details.


*** Generalized Variables With setf

The =setf= macro can be used to set an arbitrary "place", rather than just a
symbol value. The concept is similar to an *lvalue* in the C programming
language, where an lvalue refers to a location that a value can be assigned to.

The following example shows some sample uses of =setf=:

#+BEGIN_SRC emacs-lisp
  (setq my-list (list 1 2 3))     ; my-list = '(1 2 3)
  (setf (car my-list) 2)          ; my-list = '(2 2 3)
  (setf (cdr my-list) 7)          ; my-list = '(2 . 7)

  (setq my-string "hello")        ; my-string = "hello"
  (setf (aref my-string 1) ?f)    ; my-string = "hfllo"
#+END_SRC

See the documentation for more examples.


** Functions

A *function* is a rule for carrying out a computation given input values called
*arguments*. The result of the computation is the *value* or *return value* of
the function.

Emacs Lisp supports several types of functions:

- lambda expression :: A function written in Lisp.-
- primitive :: A function written in C, normally because it forms a fundamental
  part of Lisp (like =car=, =cdr=, etc.), because it uses low-level OS services,
  or for speed.
- special form :: A primitive that is like a function, but does not evaluate all
  of its arguments in the normal way (e.g., =if=, =and=, etc.)
- macro :: A construct defined in Lisp which converts a Lisp expression into
  another expression, which is then evaluated in place of the original
  expression.
- command :: A function which can be interactively invoked by the user using the
  =execute-command= primitive.
- closure :: A function like a lambda expression, which captures part of its
  surrounding lexical environment.
- byte-code-function :: A function compiled by the byte compiler.
- autoload object :: A placeholder for a real function. If the autoload object
  is called, Emacs loads the file containing the definition of the function, and
  then calls the real function.


*** Basic Syntax

A lambda expression is of the form:

#+BEGIN_SRC emacs-lisp
  (lambda (ARGS ...)
    DOCUMENTATION-STRING
    INTERACTIVE-DECLARATION
    BODY-FORMS ...)
#+END_SRC


*** Naming Functions

For convenience, a name can be associated with a function. This occurs when the
"function cell" (see [[info:elisp#Symbol%20Components][info:elisp#Symbol Components]]) of a symbol contains a
lambda expression object. This is most commonly done using the =defun= form:

#+BEGIN_SRC emacs-lisp
  (defun my-func (x)
    (+ x 1))
  (my-func 4)	; returns 5
#+END_SRC

Less commonly, this can be done with the =fset= function:

#+BEGIN_SRC emacs-lisp
  (fset 'my-func (lambda (x) (- x 1)))
  (my-func 4)	; returns 3
#+END_SRC

Note that functions and variables inhabit different namespaces in Emacs Lisp
(unlike Scheme and some other dialects). Therefore it perfectly valid to have
both a function and a variable named =foo=, both of which will be independent
from each other.


*** Execution

Named functions may be executed directly:

#+BEGIN_SRC emacs-lisp
  (defun square (x)
    (* x x))
  (square 5)    ; returns 25
#+END_SRC

Arbitrary functions (either as symbols, or as lambda functions) are executed by
passing them to the =funcall= function:

#+BEGIN_SRC emacs-lisp
  (funcall (lambda (x) (+ x 1)) 5)	; returns 6
  (funcall 'square 6)			; returns 36
#+END_SRC

The =apply= function is similar to =funcall=, except that the last argument may
be a list which is expanded to fill the function's argument list.

#+BEGIN_SRC emacs-lisp
  (apply '+ '(1 2 3 4))   ; returns 10
#+END_SRC


*** Optional Arguments

Function argument lists support optional arguments using the =&optional=
specifier. If the arguments are not specified, they default to nil.

#+BEGIN_SRC emacs-lisp
  (defun add (x &optional y)
    (or y (setq y 1))
    (+ x y))

  (add 5 10)      ; returns 15
  (add 5)         ; returns 6
#+END_SRC

Optional arguments must follow all non-optional arguments.


*** Rest Arguments

Argument lists also support variadic functions in the form of "rest" arguments,
using the =&rest= specifier. This allows the caller to specify an arbitrary
number of arguments, which are passed to the function as a list.

#+BEGIN_SRC emacs-lisp
  (defun variadic (&rest items)
    (dolist (item items)
      (insert item)))

  (variadic "this" "is" "variadic")
#+END_SRC

Rest arguments must follow all non-optional and optional named arguments.


*** Documentation Strings

Functions support "documentation strings", which is an arbitrary string
immediately after the argument list. The documentation string is displayed by
the Emacs help system when the user searches for help on a function (using the
=C-h f= keystroke).

For example:

#+BEGIN_SRC emacs-lisp
  (defun square (x)
    "Returns the square of X."
    (* x x))
#+END_SRC

There are numerous conventions which should be followed and special string
tokens which may be used when writing documentation strings. See the Emacs Lisp
manual for further details.


*** Function Aliases

The =defalias= form may be used to create an "alias" for a function. A new
documentation string may optionally be specified.

#+BEGIN_SRC emacs-lisp
  (defalias 'add '+ "Add, except spiffier.")
  (add 1 2 3)	; returns 6
#+END_SRC
